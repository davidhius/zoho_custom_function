//======================================
//== StringVietnamWorksSyncCVNow(String recId,String jobIDVNWs,String scheduleId,String latestApplyOnSynced)
//== Immediately Sync CV
//== VietnamWorksSyncCVNow
//======================================
try 
{
	listLength = "".leftpad(25).toList("");
	lastApplyOnUTCRaw = if(isNull(latestApplyOnSynced),"1970-01-01 00:00:00",latestApplyOnSynced);
	lastApplyOnUTC = lastApplyOnUTCRaw.toDateTime("yyyy-MM-dd HH:mm:ss");
	lastApplyOn = lastApplyOnUTC.addHour(7).toString("yyyy-MM-dd HH:mm:ss").toDateTime("yyyy-MM-dd HH:mm:ss");
	maxApplyOn = lastApplyOn;
	if(jobIDVNWs.isNull())
	{
		return "Thiếu thông tin.";
	}
	//get all Candidate in vietnamworks system
	vnwsApplications = Map();
	stopFetching = false;
	for each index ind in listLength
	{
		if(stopFetching)
		{
			info "Stop fetching further pages";
			break;
		}
		page = ind + 1;
		respApplicationVNWs = invokeurl
		[
			url :"https://api.vietnamworks.com/api/rest/v1/applications/" + jobIDVNWs + "?page=" + page
			type :GET
			headers:{"Content-Type":"application/json"}
			connection:"prdvietnamworks"
		];
		if(respApplicationVNWs.contains("data") && respApplicationVNWs.get("data").contains("items") && respApplicationVNWs.get("data").get("items").size() > 0)
		{
			listResp = respApplicationVNWs.get("data").get("items");
			for each  rec in listResp
			{
				candidateApplyOn = rec.get("applyOn").toDateTime("yyyy-MM-dd HH:mm:ss");
				info "candidateApplyOn ::: " + candidateApplyOn;
				info "Email ::: " + rec.get("email");
				// Nếu đã sync tới ứng viên này thì stop luôn
				if(candidateApplyOn <= lastApplyOn)
				{
					info "Reached old candidate, stop fetching. Email: " + rec.get("email");
					stopFetching = true;
					break;
				}
				// Lưu max applyOn để update cuối cùng
				if(candidateApplyOn > maxApplyOn)
				{
					maxApplyOn = candidateApplyOn;
				}
				vnwsApplications.put(rec.get("email"),rec);
			}
		}
		else
		{
			break;
		}
	}
	// Nếu không có ứng viên mới thì dừng
	if(vnwsApplications.isEmpty())
	{
		params = {"Next Schedule Execution Time":zoho.currenttime.addMinutes(120)};
		response = zoho.recruit.updateRecord("CustomModule1",scheduleId,params,true,"sys_zohorecruit");
		sendmail
		[
			from :zoho.adminuserid
			to :"hieuvn@smartosc.com"
			subject :"DEBUG RUNNING JOB UPDATE"
			message :zoho.currenttime + " ::: " + scheduleId + " ::: params ::: " + params.toText() + " ::: response ::: " + response.toText()
		]
		return "Hoàn thành. Không có thêm CV";
	}
	//get all Candidate in recruit system
	recruitApplicationsEmail = List();
	for each index ind in listLength
	{
		page = ind + 1;
		respAssociateJOID = invokeurl
		[
			url :"https://recruit.zoho.com/recruit/v2/Job_Openings/" + recId + "/associate?page=" + page
			type :GET
			connection:"sys_zohorecruit"
		];
		if(respAssociateJOID.contains("data") && respAssociateJOID.get("data").size() > 0)
		{
			listResp = respAssociateJOID.get("data");
			for each  rec in listResp
			{
				if(!rec.get("VietnamWorksEmail").isNull())
				{
					info "rec.get(\"VietnamWorksEmail\")" + rec.get("VietnamWorksEmail");
					recruitApplicationsEmail.add(rec.get("VietnamWorksEmail").toLowerCase());
				}
				else if(!rec.get("Email").isNull())
				{
					info "rec.get(\"Email\")" + rec.get("Email");
					recruitApplicationsEmail.add(rec.get("Email").toLowerCase());
				}
			}
			if(respAssociateJOID.get("info").get("more_records") == false)
			{
				break;
			}
		}
		else
		{
			break;
		}
	}
	for each  candidate in vnwsApplications
	{
		isExist = false;
		info "candidate email ::: " + candidate.get("email");
		for each  email in recruitApplicationsEmail
		{
			if(email.equalsIgnoreCase(candidate.get("email")) || candidate.get("email").equalsIgnoreCase("lehoasvn@outlook.com"))
			{
				isExist = true;
			}
		}
		if(isExist)
		{
			info "candidate email exist ::: " + candidate.get("email");
			continue;
		}
		info "candidate.get(\"linkDownload\") ::: " + candidate.get("linkDownload");
		fileResp = invokeurl
		[
			url :candidate.get("linkDownload")
			type :GET
		];
		if(fileResp.isFile())
		{
			info "filename :: " + standalone.UnicodeToVietnamese(fileResp.getFileName().tostring());
			response = invokeurl
			[
				url :"https://recruit.zoho.com/recruit/v2/Candidates/actions/import_document"
				type :POST
				parameters:{"document":zoho.encryption.base64Encode(fileResp),"filename":standalone.UnicodeToVietnamese(fileResp.getFileName().tostring())}
				connection:"sys_zohorecruit"
			];
			info "Phản hồi từ API: " + response;
			if(!isNull(response.get("details")) && !isNull(response.get("details").get(0)))
			{
				//update source
				ParamMap = Map();
				DataMap = Map();
				ListData = List();
				ParamMap.put("id",response.get("details").get(0).get("id"));
				ParamMap.put("Source","VietnamWorks");
				ParamMap.put("VietnamWorksEmail",candidate.get("email"));
				ListData.add(ParamMap);
				DataMap.put("data",ListData);
				info "DataMap update source ::: " + DataMap;
				responseUpdate = invokeurl
				[
					url :"https://recruit.zoho.com/recruit/v2/Candidates"
					type :PUT
					parameters:DataMap.toString()
					connection:"sys_zohorecruit"
				];
				info responseUpdate;
				//associate
				ParamMap = Map();
				DataMap = Map();
				ListData = List();
				ParamMap.put("ids",{response.get("details").get(0).get("id")});
				ParamMap.put("jobids",{recId});
				ListData.add(ParamMap);
				DataMap.put("data",ListData);
				info "DataMap associate ::: " + DataMap;
				response = invokeurl
				[
					url :"https://recruit.zoho.com/recruit/v2/Candidates/actions/associate"
					type :PUT
					parameters:DataMap.toString()
					connection:"sys_zohorecruit"
				];
				info response;
			}
			else
			{
				sendmail
				[
					from :zoho.adminuserid
					to :"hieuvn@smartosc.com"
					cc:"v.ngabth4@vinfast.vn"
					subject :"VFxVNWs Import Document Error"
					message :recId + " ::: " + jobIDVNWs + " ::: " + scheduleId + " <br> ::: item -- " + candidate.toText() + " <br> ::: response -- " + response.toText()
				]
			}
		}
	}
	info {"Next Schedule Execution Time":zoho.currenttime.addMinutes(120),"Latest ApplyOn Synced":maxApplyOn};
	//update next time to run this function
	params = {"Next Schedule Execution Time":zoho.currenttime.addMinutes(120),"Latest ApplyOn Synced":maxApplyOn};
	response = zoho.recruit.updateRecord("CustomModule1",scheduleId,params,true,"sys_zohorecruit");
}
catch (e)
{
	response = zoho.recruit.updateRecord("CustomModule1",scheduleId,{"Next Schedule Execution Time":zoho.currenttime.addMinutes(120),"Exceptions Info":e},true,"sys_zohorecruit");
	throws "Lỗi trong quá trình đồng bộ. Vui lòng thử lại.";
}
return "Hoàn thành.";
